import Math;
import TwoCansAlpha4;

enum TitlePhase {
	WAIT = 1,
	INHALE = 2,
	CHEW = 3,
}

class TitleScene : AbstractScene {
	field counter;
	field phase;
	field spacebar;
	field mouth;
	
	constructor() : base() {
		this.counter = 0;
		this.phase = TitlePhase.WAIT;
		this.spacebar = [63, 166];
		this.mouth = [280, 241];
	}
	
	function update(inputManager, events) {
		
		this.counter++;
		space_pressed = false;
		for (event : events) {
			if (event.key == KeyboardKey.SPACE && event.down) {
				space_pressed = true;
			}
		}
		
		switch (this.phase) {
			case TitlePhase.WAIT:
				if (space_pressed) {
					this.counter = 0;
					this.phase = TitlePhase.INHALE;
				}
				break;
			case TitlePhase.INHALE:
				mx = this.mouth[0];
				my = this.mouth[1];
				sx = this.spacebar[0];
				sy = this.spacebar[1];
				dx = mx - sx;
				dy = my - sy;
				dist = (dx ** 2 + dy ** 2) ** .5;
				v = 10;
				dx = v * dx / dist;
				dy = v * dy / dist;
				if (dist <= v) {
					this.phase = TitlePhase.CHEW;
				}
				
				this.spacebar[0] = sx + dx;
				this.spacebar[1] = sy + dy;
				break;
			case TitlePhase.CHEW:
				if (this.counter == FPS * 2) {
					this.switchScene(new FadeTransitionScene(this, new PlayScene(get_next_level(null))));
				}
				break;
			default: break;
		}
	}
	
	function render() {
		rc = get_render_counter();
		title = Images.get('images/title.png');
		title.draw(0, 0);
		mouthx = this.mouth[0];
		mouthy = this.mouth[1];
		spacex = this.spacebar[0];
		spacey = this.spacebar[1];
		show_text = true;
		switch (this.phase) {
			case TitlePhase.WAIT:
				path = 'mouth1.png';
				show_text = (rc / 10) % 2 == 1;
				break;
			case TitlePhase.INHALE:
				path = 'mouth1.png';
				break;
			case TitlePhase.CHEW:
				show_text = false;
				path = ((rc / 8) % 2) == 1
					? 'mouth2.png'
					: 'mouth3.png';
					
				break;
			default:
				break;
		}
		
		img = Images.get('images/' + path);
		img.draw(mouthx - img.width / 2, mouthy - img.height / 2);
		
		if (show_text) {
			img = Images.get('images/spacebar.png');
			img.draw(Math.floor(spacex - img.width / 2), Math.floor(spacey - img.height / 2));
		}
	}
}
