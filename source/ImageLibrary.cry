import Graphics;
import Math;

class ImageLibrary {
	static field images = {};
	static field flipped = {};
	static field by_scale = {};
	static field flipped_by_scale = {};
	static field at_height = {};
	static field imageSheet = null;
	
	static function get_scaled(path, scale, flipped = false) {
		scaleint = Math.floor(scale * 100);
		lookup = flipped ? ImageLibrary.flipped_by_scale : ImageLibrary.by_scale;
		images = lookup.get(scaleint, null);
		if (images == null) {
			images = {};
			lookup[scaleint] = images;
		}
		
		img = images.get(path, null);
		if (img == null) {
			img = ImageLibrary.get(path, flipped);
			img = img.scale(Math.floor(img.width * scale), Math.floor(img.height * scale));
			images[path] = img;
		}
		return img;
	}
	
	static function get_at_height(path, height) {
		lookup = ImageLibrary.at_height.get(height, null);
		if (lookup == null) {
			lookup = {};
			ImageLibrary.at_height[height] = lookup;
		}
		
		img = lookup.get(path, null);
		if (img == null) {
			img = ImageLibrary.get(path, false);
			if (img.height != height) {
				r = 1.0 * height / img.height;
				img = img.scale(Math.floor(r * img.width), Math.floor(r * img.height));
			}
			lookup[path] = img;
		}
		
		return img;
	}
	
	static function get(path, flipped = false) {
		
		lookup = flipped ? ImageLibrary.flipped : ImageLibrary.images;
		img = lookup.get(path, null);
		if (img == null) {
			if (flipped) {
				img = ImageLibrary.get(path, false);
				img = img.flip(true, false);
				ImageLibrary.flipped[path] = img;
			} else {
				rpath = 'images/' + path;
				rawImg = ImageLibrary.imageSheet.getImage(rpath);
				img = Graphics.GraphicsTexture.createImage(rawImg);
				ImageLibrary.images[path] = img;
			}
		}

		return img;
	}
}
