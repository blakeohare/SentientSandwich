GRAVITY = .1;
TERMINAL_VELOCITY = .9;

class Sprite {
	constructor(type, x, y) {
		this.x = x + 0.0;
		this.y = y + 0.0;
		this.type = type;
		// TODO: better sprite definition system.
		this.width = .75;
		if (type == 'player') {
			this.height = .75;
		} else {
			this.height = 1.0;
		}
		this.ground = null;
		this.vy = 0;
	}
	
	function update(scene, grid) {
		
		// Phase 1: apply y vector
		
		effective_height = this.height * .9;
		
		if (this.ground != null) {
			// you are standing on ground. set Y to the ground value.
			this.y = this.ground.y;
			this.vy = 0;
		} else {
			this.vy += GRAVITY;
			if ($abs(this.vy) > TERMINAL_VELOCITY) {
				this.vy = (this.vy < 0) ? -TERMINAL_VELOCITY : TERMINAL_VELOCITY;
			}
			new_y = this.y + this.vy;
			col = $floor(this.x);
			if (this.vy < 0) {
				// you are flying up in the air
				// check to see if you bonk your head.
				top_y = new_y - effective_height;
				row = $floor(top_y);
				tile = grid[col][row];
				if (tile.blocking) {
					// BONK!
					this.vy = 0;
					this.y = row + 1 + effective_height + EPSILON;
				} else {
					this.y = new_y;
				}
			} else {
				// you are falling down
				row = $floor(new_y);
				tile = grid[col][row];
				if (tile.blocking) {
					this.y = tile.y + 0.0;
					this.ground = tile;
				} else {
					this.y = new_y;
				}
			}
		}
	}
	
	function render(rc, offset_x, offset_y) {
		
		width = $floor(this.width * 40);
		left = $floor(this.x * 40 - width / 2);
		bottom = $floor(this.y * 40);
		height = $floor(this.height * 40);
		top = bottom - height;
		
		blue = this.type == 'player' ? 0 : 255;
		$gfx_draw_rectangle(left - offset_x, top - offset_y, width, height, 255, 0, blue, 255);
	}
}